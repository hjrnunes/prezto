#
# A single line theme with Git information on the left and Ruby on the right.
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Yan Pritzker <yan@pritzker.ws>
#
# Screenshots:
#   http://i.imgur.com/gLgVp6Y.png
#

# Load dependencies.
pmodload 'helper'

function prompt_htheme_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  # Format PWD.
  _prompt_htheme_pwd=$(prompt-pwd)
  
  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Get Python information.
  if (( $+functions[python-info] )); then
    python-info
  fi

  # Get Ruby information.
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi
}

function prompt_htheme_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook to set up prompt parameters before each command.
  add-zsh-hook precmd prompt_htheme_precmd

  # Tell prezto we can manage this prompt
  zstyle ':prezto:module:prompt' managed 'yes'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format "%F{81}%b%f"
  zstyle ':prezto:module:git:info:added' format "→%f"
  zstyle ':prezto:module:git:info:deleted' format "%F{166}●%f"
  zstyle ':prezto:module:git:info:modified' format "⚡"
  zstyle ':prezto:module:git:info:untracked' format "%F{135}●%f"
  zstyle ':prezto:module:git:info:ahead' format "↑%f"
  zstyle ':prezto:module:git:info:behind' format "↓%f"
  zstyle ':prezto:module:git:info:stashed' format "↩%f"
  zstyle ':prezto:module:git:info:keys' format 'prompt' '(%b%d%a%m%u) '

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '%f%F{3}(%v)%F{7} '

  # Set ruby-info parameters.
  zstyle ':prezto:module:ruby:info:version' format '[%v]'

  _prompt_htheme_pwd=''

  # Define prompts.
  # PROMPT="%F{10}%n%f@%F{221}%m%f %F{green}%~%f "
  PROMPT='%F{10}%n%f@%F{221}%m%f %F{green}${_prompt_htheme_pwd}%f '
  
  PROMPT+='${git_info:+${(e)git_info[prompt]}}'
  PROMPT+='$python_info[virtualenv]'
  PROMPT+="%F{221}> "
}

prompt_htheme_setup "$@"
# vim: ft=zsh
